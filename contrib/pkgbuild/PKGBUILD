# Maintainer: Alex Minghella <a@minghella.net>
pkgname=syncup
pkgver=0.2.0
pkgrel=1
pkgdesc="Synchronise a local maildir store with a remote"
arch=('any')
license=('custom:0BSD')
depends=('isync' 'notmuch-runtime')
makedepends=('asciidoctor' 'lzip' 'make')
options=('zipman')
source=("https://git.minghella.net/pub/scm/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.lz")
sha256sums=('3cf6f210aa1fd41dc3f73d5784225d4cac996711428f06828f63d2714adf83d1')

build()
{
	cd "${pkgname}-${pkgver}"
	make PREFIX="/usr"
}

package()
{
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}/" PREFIX="/usr" install
	install -D -m 0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
